grammar it.rcpvision.emf.components.dsl.EmfComponentsDsl with
    org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

generate model "http://www.rcpvision.it/emf/components/dsl/EmfComponentsDsl"

Model:
	imports+=Import*
	module = Module;
	
Import:
  'import' importedNamespace = QualifiedNameWithWildcard
;
 
QualifiedNameWithWildcard:
  QualifiedName '.*'?
;

Module:
	'module' name=QualifiedName '{'
		(
			(labelProvider = LabelProvider)?
			&
			(propertyDescriptionProvider = PropertyDescriptionProvider)?
			&
			(featureProvider = FeatureProvider)?
		)
	'}'
;

LabelProvider:
	{LabelProvider} 'labelProvider' '{'
		((
		'labels' '{'
			labelSpecifications+=LabelSpecification*
		'}'
		)?
		&
		(
		'images' '{'
			imageSpecifications+=LabelSpecification*
		'}'
		)?)
	'}'
;

LabelSpecification:
	parameterType=JvmTypeReference (name=ValidID)?
	'->'
	expression = XExpression
;

PropertyDescriptionProvider:
	{PropertyDescriptionProvider} 'featureLabelProvider' '{'
		((
		'labels' '{'
			labelSpecifications+=PropertyDescriptionSpecification*
		'}'
		)?)
	'}'
;

PropertyDescriptionSpecification:
	parameterType=[types::JvmType|QualifiedName] ':' feature=XFeatureCall
	'->'
	expression = XExpression
;

FeatureProvider:
	{FeatureProvider} 'featureProvider' '{'
		((
		'features' '{'
			featureSpecifications+=FeatureSpecification*
		'}'
		)?)
	'}'
;

FeatureSpecification:
	parameterType=[types::JvmType|QualifiedName] 
	'->'
	features += XFeatureCall (',' features += XFeatureCall)*
;

// a utility base class for rules which access features of an EClass
EmfFeatureAccess: PropertyDescriptionSpecification | FeatureSpecification;
