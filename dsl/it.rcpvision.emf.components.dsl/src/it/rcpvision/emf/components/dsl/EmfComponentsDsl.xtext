grammar it.rcpvision.emf.components.dsl.EmfComponentsDsl with
    org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

generate model "http://www.rcpvision.it/emf/components/dsl/EmfComponentsDsl"

Model:
	imports+=Import*
	module = Module;
	
Import:
  'import' importedNamespace = QualifiedNameWithWildcard
;
 
QualifiedNameWithWildcard:
  QualifiedName '.*'?
;

Module:
	'module' name=QualifiedName '{'
		(
			(labelProvider = LabelProvider)?
			&
			(propertyDescriptionProvider = PropertyDescriptionProvider)?
			&
			(featuresProvider = FeaturesProvider)?
			&
			(formFeatureControlFactory = FormFeatureControlFactory)?
		)
	'}'
;

LabelProvider:
	{LabelProvider} 'labelProvider' '{'
		((
		'labels' '{'
			labelSpecifications+=LabelSpecification*
		'}'
		)?
		&
		(
		'images' '{'
			imageSpecifications+=LabelSpecification*
		'}'
		)?)
	'}'
;

LabelSpecification:
	parameterType=JvmTypeReference (name=ValidID)?
	'->'
	expression = XExpression
;

PropertyDescriptionProvider:
	{PropertyDescriptionProvider} 'propertyDescriptionProvider' '{'
		((
		'labels' '{'
			labelSpecifications+=PropertyDescriptionSpecification*
		'}'
		)?)
	'}'
;

PropertyDescriptionSpecification:
	parameterType=JvmTypeReference ':' feature=XFeatureCall
	'->'
	expression = XExpression
;

FeaturesProvider:
	{FeaturesProvider} 'featuresProvider' '{'
		((
		'features' '{'
			featureSpecifications+=FeatureSpecification*
		'}'
		)?)
	'}'
;

FeatureSpecification:
	parameterType=JvmTypeReference 
	'->'
	features += XFeatureCall (',' features += XFeatureCall)*
;

FormFeatureControlFactory:
	{FormFeatureControlFactory} 'formFeatureControlFactory' '{'
		((
		'controls' '{'
			controlSpecifications+=FormFeatureControlSpecification*
		'}'
		)?)
	'}'
;

FormFeatureControlSpecification:
	parameterType=JvmTypeReference ':' feature=XFeatureCall
	'->'
	expression = XExpression
	('target' target = XExpression)?
;

// a utility base class for rules which access features of an EClass
EmfFeatureAccess: 
	PropertyDescriptionSpecification |
	FeatureSpecification |
	FormFeatureControlSpecification
;
