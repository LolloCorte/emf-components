grammar it.rcpvision.emf.components.dsl.EmfComponentsDsl with
    org.eclipse.xtext.xbase.Xbase

generate model "http://www.rcpvision.it/emf/components/dsl/EmfComponentsDsl"

Model:
	imports+=Import*
	module = Module;
	
Import:
  'import' importedNamespace = QualifiedNameWithWildcard
;
 
QualifiedNameWithWildcard:
  QualifiedName '.*'?
;

Module:
	'module' name=QualifiedName '{'
		(
			(labelProvider = LabelProvider)?
			&
			(featureLabelProvider = FeatureLabelProvider)?
		)
	'}'
;

LabelProvider:
	{LabelProvider} 'labelProvider' '{'
		((
		'labels' '{'
			labelSpecifications+=LabelSpecification*
		'}'
		)?
		&
		(
		'images' '{'
			imageSpecifications+=LabelSpecification*
		'}'
		)?)
	'}'
;

LabelSpecification:
	parameterType=JvmTypeReference (name=ValidID)?
	'->'
	expression = XExpression
;

FeatureLabelProvider:
	{FeatureLabelProvider} 'featureLabelProvider' '{'
		((
		'labels' '{'
			labelSpecifications+=FeatureLabelSpecification*
		'}'
		)?)
	'}'
;

FeatureLabelSpecification:
	parameterType=JvmTypeReference ':' feature=XFeatureCall
	'->'
	expression = XExpression
;
